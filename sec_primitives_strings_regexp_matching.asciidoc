[[sec_primitives_strings_re_find]]
==== Searching a String by Pattern

===== Problem

You need to test a string to see if parts of it match a pattern.

===== Solution

To check for the presence of a pattern inside a string, use +re-find+.

[source,clojure]
----
;; Any contiguous groups of numbers
(re-find #"\d+" "I've just finished reading Fahrenheit 451")
;; -> "451"

(re-find #"Bees" "Beads aren't cheap.")
;; -> nil
----

===== Discussion

+re-find+ is quite handy for quickly testing a string for the presence
of a pattern. It takes a regular expression pattern and a string, then
returns either the first match of that pattern or nil.

If your criterion is more stringent and you require the _entire_
string match a pattern use +re-matches+. Unlike +re-find+,
which matches any portion of a string, +re-matches+ matches if and
only if the *entire* string matches the pattern.

[source,clojure]
----
;; In find, #"\w+" is any contiguous word characters 
(re-find #"\w+" "my-param")
;; -> "my"

;; But in matches #"\w+" means "all word characters"
(re-matches #"\w+" "my-param")
;; -> nil

(re-matches #"\w+" "justLetters")
;; -> "justLetters"
----

===== See Also

* <<sec_primitives_strings_re_matches>> for information on extracting
  values from a string using regular expressions.
