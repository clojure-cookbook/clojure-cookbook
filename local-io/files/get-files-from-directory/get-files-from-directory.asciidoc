[[sec_local-io_files_get_files_from_dir]]
=== Listing Files in a Directory
[role="byline"]
originally submitted by Stefan Karlsson

==== Problem

Given a directory, you want to access the files inside

==== Solution

Call the built-in +file-seq+ function to return a lazy sequence
of +java.io.File+ objects.

[NOTE]
====
To follow along with this recipe, create some sample files and folders
using these commands (on Linux or Mac):

[source,console]
----
$ mkdir -p next-gen
$ touch next-gen/picard.jpg next-gen/locutus.bmp next-gen/data.txt
----
====

[source,clojure]
----
(def tng-dir (file-seq (clojure.java.io/file "./next-gen")))

tng-dir
;; -> (#<File ./next-gen>
;;     #<File ./next-gen/picard.jpg>
;;     #<File ./next-gen/locutus.bmp>
;;     #<File ./next-gen/data.txt>)
----

==== Discussion

Sequences are one of Clojure's more powerful abstractions; treating a
directory hierarchy as a squence allows you to leverage functions like
+map+ and +filter+ to manipulate files and directories.

Consider, for example, the case where you would like to select only files in a
directory hierarchy (and not directories). You can define such a function
by taking a sequence of files and directories and filtering them by the
+.isFile+ property of +java.io.File+ objects.

[source,clojure]
----
(defn only-files
  "Filter a sequence of files/directories by the .isFile property of
  java.io.File"
  [file-s]
  (filter #(.isFile %) file-s))

(only-files tng-dir)
;; -> (#<File ./next-gen/data.txt>
;;     #<File ./next-gen/locutus.bmp>
;;     #<File ./next-gen/picard.jpg>)
----

What if you wanted display the string-names of all those files? Define
a +names+ function to map the +.getName+ property over a sequence of
files, combining +only-files+ and +names+ to get a list of file names
in a directory.

[source,clojure]
----
(defn names
  "Return the .getName property of a sequence of files"
  [file-s]
  (map #(.getName %) file-s))

(-> tng-dir
    only-files
    names)
;; -> ("data.txt" "locutus.bmp" "picard.jpg")
----

==== See Also

* The documentation for the
  http://docs.oracle.com/javase/7/docs/api/java/io/File.html[File
  class] for a complete list of properties and methods available on
  +File+ objects.
* Combine these techniques with utility libraries like Google Guava's
  http://docs.guava-libraries.googlecode.com/git/javadoc/com/google/common/io/Files.html[Files
  class] or Apache Commons
  http://commons.apache.org/proper/commons-io/javadocs/api-1.4/org/apache/commons/io/FilenameUtils.html[FilenameUtils class]
  to exert even greater leverage over the file sequence abstraction.

