[[sec_primitives_dates_formatting_dates]]
=== Formatting Dates Using clj-time
[role="byline"]
by Ryan Neufeld

==== Problem

You need to print dates or times in a particular format.((("dates/times", "formatting")))

==== Solution

While it is possible to format Java date&#x2013;like instances (+Date+,
+Calendar+, and +Timestamp+) with +clojure.core/format+, you should
use https://github.com/clj-time/clj-time[+clj-time+] to format dates.(((Java, date/time classes)))

Before starting, add `[clj-time "0.6.0"]` to your project's
dependencies or start a REPL using +lein-try+:

[source,shell-session]
----
$ lein try clj-time
----

To output a date/time as a string, use +clj-time.format/unparse+ with a
+DateTimeFormatter+. There are a number of built-in formatters available via
+clj-time.format/formatters+, or you can build your own with
+clj-time.format/formatter+:

[source,clojure]
----
(require '[clj-time.format :as tf])
(require '[clj-time.core :as t])

(tf/unparse (tf/formatters :date) (t/now))
;; -> "2013-04-06"

(def my-format (tf/formatter "MMM d, yyyy 'at' hh:mm"))
(tf/unparse my-format (t/now))
;; -> "Apr 6, 2013 at 04:54"
----

==== Discussion

It is certainly possible to format pure Java dates and times; however, in our
experience, it isn't worth the hassle--the syntax is ugly, and the workflow is verbose. +clj-time+ and its backing library Joda-Time have a track
record for making it easy to work with dates and times on the JVM.(((Joda-Time library)))(((functions, formatter)))

The +formatter+ function is quite the gem. Not only does it produce a
"format" capable of printing or ++unparse++ing a date, but it is also
capable of parsing strings back into dates. In other words,
+DateTimeFormatter+ is capable of round-tripping from string to +Date+
and back again. Much of how +formatter+ and +formatters+ work is
 covered in <<sec_primitives_dates_parsing_dates>>.

One format symbol used less frequently in parsing is the textual
day of the week (i.e., "Tuesday" or "Tue"). Use "++E++" in your format
string to output the abbreviated day of the week, and "++EEEE++" for the
full-length day of the week:

[source,clojure]
----
(def abbr-day (tf/formatter "E"))
(def full-day (tf/formatter "EEEE"))

(tf/unparse abbr-day (t/now))
;; -> "Mon"
(tf/unparse full-day (t/now))
;; -> "Monday"
----

If you need to format native Java date/time instances, you can use the
functions in the +clj-time.coerce+ namespace to coerce any number of
Java date/time instances into Joda-Time instances:

[source,clojure]
----
(require '[clj-time.coerce :as tc])

(tc/from-date (java.util.Date.))
;; -> #<DateTime 2013-04-06T17:03:16.872Z>
----

Similarly, you can use +clj-time.coerce+ to coerce instances from
Joda-Time instances into other formats:

[source,clojure]
----
(tc/to-date (t/now))
;; -> #inst "2013-04-06T17:03:57.239-00:00"

(tc/to-long (t/now))
;; -> 1365267761585
----

==== See Also

* The +clj-time+ https://github.com/clj-time/clj-time[project page] on
  GitHub

* <<sec_primitives_dates_parsing_dates>> for more detailed
  information on +formatter+ and +formatters+

* The official
  http://docs.oracle.com/javase/7/docs/api/java/text/SimpleDateFormat.html[API
  documentation] for Java's simple date formatter

