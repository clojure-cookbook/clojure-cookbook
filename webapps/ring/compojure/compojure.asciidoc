[[sec_webapps_compojure_compojure]]
=== Routing Requests with Compojure
[role="byline"]
by Adam Bard

==== Problem

You want an easy way to route URLs to specific Ring handler functions.

==== Solution

Use the Compojure library (https://github.com/weavejester/compojure) to add routing to your app.

To follow along with this recipe, clone the _https://github.com/clojure-cookbook/ringtest_ repository and overwrite _src/ringtest.clj_.

._src/ringtest.clj_
[source, clojure]
----
(ns ringtest
  (:require
    [compojure.core :refer [defroutes GET]]
    [ring.adapter.jetty :as jetty]))

; View functions
(defn view [x]
  (str "<h1>" x "</h1>"))

(defn index []
  (view "Hello"))

(defn index-fr []
  (view "Bonjour"))

; Routing
(defroutes main-routes
  (GET "/" [] (index))
  (GET "/en/" [] (index))
  (GET "/fr/" [] (index-fr))
  (GET "/:greeting/" [greeting] (view greeting)))

; Server
(defn -main []
 (jetty/run-jetty main-routes {:port 3000}))
----

==== Discussion

Compojure is a routing library that lets you define routes for your app.
It does this via the `defroutes` macro, which produces a Ring handler function.

Here, we define four routes:

* `/`, which displays "Hello"
* `/en/`, the same
* `/fr/`, which displays "Bonjour", and
* `/:greeting/`, which echos the greeting passed.

The last view is an example of Compojure's URL parameter syntax. The section
of the URL identified by `:greeting` is passed along to the view, which displays
it for the user. So, visiting `http://localhost:3000/Buenos%20Dias/` displays
"Buenos Dias" in response.

One caveat to be aware of is that Compojure routes are sensitive to a
trailing slash: a route defined as `/users/:user/blog/` will not match
the url `http://mysite.com/users/fred/blog` - but it *will* match
`http://mysite.com/users/fred/blog/`.

The `[]` in each route is actually a syntactic sugar for intercepting these
parameters. You can also use `req` or any other symbol to get the whole request:

[source, clojure]
----
(defroutes main-routes-2
  (GET "/" req (some-view req)))
----

You can even use Clojure's destructuring syntax to extract parts of the request.
For example, if you're using the `wrap-params` middleware, you can grab the params
and pass them to a function:

[source, clojure]
----
(defroutes main-routes-2
  (GET "/" {params :params} (some-other-view params)))
----

It is important to realize that Compojure works on top of Ring. Your views
should still return Ring response maps (although Compojure will wrap strings
you return with a basic 200 response).

You can also define routes for other types of HTTP requests by
specifying the route using the relevant Compojure directive:
`compojure.core/POST` and `compojure.core/PUT` are most commonly used
in addition to `compojure.core/GET`.

Compojure provides a few other helpful tools:
http://weavejester.github.io/compojure/compojure.route.html[`compojure.routes`],
which provides helpers for serving resources, files and 404 responses; and
http://weavejester.github.io/compojure/compojure.handler.html[`compojure.handler`],
which bundles a number of ring middlewares into one convenient wrapper.
