=== Splitting a string into parts
[role="byline"]
by Ryan Neufeld

==== Problem

You need to split a string into a number of parts.

==== Solution

Use +clojure.string/split+ to tokenize a string into a vector of tokens. +split+ takes two arguments, a string to tokenize and a regular expression to split on.

[source,clojure]
----
(clojure.string/split "HEADER1,HEADER2,HEADER3" #",")
;; -> ["HEADER1" "HEADER2" "HEADER3"]

(clojure.string/split "Spaces   Newlines\n\n" #"\s+")
;; -> ["Spaces" "Newlines"]
----

==== Discussion

In addition to just naively splitting on a regular expression +split+
allows you to control how many (or how few) times to split the
provided string. You can control this with the optional +limit+
argument. The most obvious effect of +limit+ is to limit the number
values returned in the resulting collection. That said, +limit+
doesn't always work like you would expect, and even the absence of
this argument carries a meaning.

Without +limit+, the +split+ function will return every possible
delimitation, but exclude any trailing empty matches.

[source,clojure]
----
;; Splitting on whitespace without an explicit limit performs an implicit trim
(clojure.string/split "field1    field2 field3   " #"\s+")
;; -> ["field1" "field2" "field3"]
----

If you indeed do want absolutely every match, including trailing empty ones, the you can specify +-1+ as the limit. 

[source,clojure]
----
;; In CSV parsing an empty match at the end of a line is still a meaningful one
(clojure.string/split "ryan,neufeld," #"," -1)
;; -> ["ryan" "neufeld" ""]
----

Specifying some other positive number as a +limit+ will cause +split+ to return at maximum +limit+ sub-strings.

[source,clojure]
----
(def data-delimiters #"[ :-]")

;; No-limit split on any delimiter
(clojure.string/split "2013-04-05 14:39" data-delimiters)
;; -> ["2013" "04" "05" "14" "39"]

;; Limit of 1 - functionally: return this string in a collection
(clojure.string/split "2013-04-05 14:39" data-delimiters 1)
;; -> ["2013-04-05 14:39"]

;; Limit of 2
(clojure.string/split "2013-04-05 14:39" data-delimiters 2)
;; -> ["2013" "04-05 14:39"]

;; Limit of 100
(clojure.string/split "2013-04-05 14:39" data-delimiters 100)
;; -> ["2013" "04" "05" "14" "39"]

----

