=== Creating a Map

==== Problem

You want to create an association which maps keys to values. You
possibly want to maintain a specific ordering of keys.

==== Solution

Use map literals (curly braces) with alternating keys and values to
create simple maps.

[source,clojure]
----
{:name ""
 :class :barbarian
 :race :half-orc
 :level 20
 :skills [:bashing :hacking :smashing]}
----

Keys and values can be any type. Commas may be used to delimit
key-value pairs where the structure would be hard to discern at a
glance.

[source,clojure]
----
{1 1, 8 64, 2 4, 9 81}
----

TIP: In Clojure, commas are whitespace, which means that they can be
used *anywhere* in a form with no effect on the value; it is just one
way to make your code easier to read.

Create an empty, unsorted map with a pair of braces: +{}+.

Create specific types of maps with map constructor
functions. +array-map+, +hash-map+, and +sorted-map+ each return a map
of the corresponding type.

[source,clojure]
----
(array-map)
;; -> {}

(sorted-map :key1 "val1" :key2 "val2")
;; -> {:key1 "val1" :key2 "val2"}
----

If a key occurs multiple times in the argument list, the last value
will be that used in the final return map.

Use +sorted-map-by+ to create a sorted map using a custom comparator.

// TODO cross-reference recipe on sorting for more info on comparators

[source,clojure]
----
(sorted-map-by #(< (count %1) (count %2))
               "pigs" 14
               "horses" 2
               "elephants" 1
               "manatees" 3)
;; -> {"pigs" 14, "horses" 2, "manatees" 3, "elephants" 1}
----

==== Discussion

Clojure maps can have one of three distinct concrete implementations.

* Array maps, +clojure.lang.PersistentArrayMap+. These are backed by a
  simple array. These are efficient for very small maps, but not for
  larger sizes.

* Hash maps, +clojure.lang.PersistentHashMap+. These are backed by a
  hash table data structure. Hash tables support near constant-time
  lookup and insertion, but also require a certain amount of overhead
  space, using up slightly more heap space.

* Sorted maps, +clojure.lang.PersistentTreeMap+. These are backed by a
  balanced red-black binary tree. They are more space efficient than
  hash maps, but have slower insertion and access times.

Array maps are the default implementation for small maps (under ten
entries at the time of writing), and hash maps are the default for
larger ones. Sorted maps can only be created by explicitly invoking
the +sorted-map+ or +sorted-map-by+ functions.

Using +assoc+ or +conj+ on a sorted map will always yield another
sorted map. However, +assoc+-ing on to an array map will yield a hash
map once it reaches a certain size. The inverse is not true; using
+dissoc+ on a hash map will not yield an array map, even if it becomes
very small.

==== See also

* Creating a set (x-ref)
* Comparators